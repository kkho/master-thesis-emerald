head	1.2;
access;
symbols
	105alpha:1.2
	104alpha:1.2
	103alpha:1.2
	102alpha:1.2
	101alpha:1.2
	100alpha:1.2
	DIKU_0:1.2;
locks; strict;
comment	@# @;


1.2
date	93.06.18.00.32.30;	author mueller;	state Exp;
branches;
next	1.1;

1.1
date	93.06.15.01.54.48;	author mueller;	state Exp;
branches;
next	;


desc
@set environment variables and path to find correct Emerald tools
for the current architecture.
@


1.2
log
@added -s switch to support producing sh compatible output
@
text
@#!/bin/csh -f
#
# setarch
#
# Script to modify Emerald-related environment variables and user's
# search path to reflect the specified architecture or the default
# architecture if none is specified.
#
# The default output of this script is to be executed by the C-shell or
# close relative.  Use the following to hide the need for an explicit eval:
#   alias emarch 'eval `$(EMERALDARCH)/bin/setarch \!*`'
#
# Usage (assuming the above alias:)
#   emarch [-q] [-s] [architecture]
#
#   -q  --  emarch will not display informational messages
#   -s  --  emarch output will be in sh format
#
# This script and setuser are closely related and any modifications
# should be made to both in parallel.
#
# ToDo:
# - would be better if the modified emerald directories were inserted
#   at the same place in the search path as any old ones were, rather
#   than simply prepending them
# 

if (! $?EMERALDROOT) then
    echo "echo Must setenv EMERALDROOT first.;"
    exit 1
endif

# parse options
while (.$1 =~ .-*)
    if (.$1 == .-q) then
        set quiet
    else if (.$1 == .-s) then
        set shformat
    else
	echo "echo Unknown option $1 ignored;"
    endif
    shift
end

if ($#argv >= 1) then
    set arch = $1
else
    set arch = `arch`
endif

if (.$arch == .) then
    echo "echo Cannot disable architecture-specific stuff.;"
    exit 1
else if (! -r $EMERALDROOT/bin/$arch) then
    echo "echo Architecture $arch does not exist.;"
    exit 1
else
    if (! $?shformat) then
      echo "setenv EMERALDARCH $arch;"
    else
      echo "EMERALDARCH=$arch;"
      echo "export EMERALDARCH;"
    endif
    set archpath = $EMERALDROOT/bin/$arch
endif

if ($?EMERALDUSER) then
    set userpath = $EMERALDROOT/bin/$EMERALDUSER
else
    set userpath = 
endif

# generate a new search path to look in directories appropriate for the
# specified architecture.  first, remove all references to paths in
# $EMERALDROOT from the existing path
if (! $?shformat) then
    set newpath = `echo $path|tr " " "\012"|grep -v $EMERALDROOT|tr "\012" " "`
else
    set newpath = `echo $PATH|tr ":" "\012"|grep -v $EMERALDROOT|tr "\012" " "`
endif

# now, prepend the appropriate paths to the remainder of the existing path
set newpath = ( $userpath $archpath $EMERALDROOT/bin $newpath )

if (! $?shformat) then
    echo "set path = ( $newpath );"
else
    set newpath = `echo $newpath | tr " " ":"`
    echo "PATH=$newpath;"
    echo "export PATH;"
endif

if (! $?quiet) then
    echo "echo Emerald architecture is now $arch;"
endif

exit 0

@


1.1
log
@Initial revision
@
text
@d9 2
a10 2
# The output of this script is to be executed by the C-shell or close
# relative.  Use the following to hide the need for an explicit eval:
d14 1
a14 1
#   emarch [-q] [architecture]
d17 1
d29 1
a29 1
    echo "echo Must setenv EMERALDROOT first."
d33 9
a41 2
if (.$1 == .-q) then
    set quiet
d43 1
a43 1
endif
d52 1
a52 1
    echo "echo Cannot disable architecture-specific stuff."
d55 1
a55 1
    echo "echo Architecture $arch does not exist."
d58 6
a63 1
    echo "setenv EMERALDARCH $arch;"
d76 5
a80 1
set newpath = `echo $path |tr " " "\012" |grep -v $EMERALDROOT |tr "\012" " "`
d85 7
a91 1
echo "set path = ( $newpath );"
@
